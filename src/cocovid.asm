	NAM	CoCoVid
	TTL	Video player for CoCo3

LOAD	EQU	$0E00		Actual load address for binary

VIDOFF	EQU	$FF90
PALOFF	EQU	$FFB0
SAMR1ST	EQU	$FFD9

	ORG	LOAD

* Init for video mode, set video buffer to $2000
* (Assumes default MMU setup...)
VIDINIT	FCB	$4C,$00,$00,$00,$00,$00,$00,$00
	FCB	$82,$12,$00,$00,$0F,$E4,$00,$00
ENDVINT	EQU	*

* Init for palette regs
PALINIT	FCB	$00,$08,$10,$18,$20,$28,$30,$38
	FCB	$07,$0F,$17,$1F,$27,$2F,$37,$3F
ENDPINT	EQU	*

EXEC	EQU	*

* High-speed poke...definitely...
	STA	SAMR1ST

	LDX	#VIDINIT
	LDY	#VIDOFF
VINTLOP	LDA	,X+
	STA	,Y+
	CMPX	#ENDVINT
	BNE	VINTLOP

	LDX	#PALINIT
	LDY	#PALOFF
PINTLOP	LDA	,X+
	STA	,Y+
	CMPX	#ENDPINT
	BNE	PINTLOP

PAINT	LDX	#$2000
PAINT1	LDA	$FFE1
	BNE	PAINT2
	JSR	[$A000]
	BEQ	PAINT1
	CMPA	#$03
	BEQ	EXIT
	BRA	PAINT1
PAINT2	LDA	$FFE0
* check for escape char
	TFR	A,B
	ANDB	#$C0
	CMPB	#$C0
	BNE	PAINT3
	BRA	PIXMWR
PAINT3	STA	,X+
PAINT4	CMPX	#$3800
	BNE	PAINT1
	BRA	PAINT

PIXMWR	TFR	A,B
	ANDB	#$3F
PIXMWR1	LDA	$FFE1
	BEQ	PIXMWR1
	LDA	$FFE0
PIXMWR2	STA	,X+
	DECB
	BNE	PIXMWR2
	JMP	PAINT4

EXIT	JMP	[$FFFE]

	END	EXEC
